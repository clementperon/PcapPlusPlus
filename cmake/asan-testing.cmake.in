set(CTEST_SOURCE_DIRECTORY  "@CMAKE_SOURCE_DIR@")
set(CTEST_BINARY_DIRECTORY  "@CMAKE_BINARY_DIR@")
set(CTEST_CMAKE_GENERATOR   "@CMAKE_GENERATOR@")
set(CTEST_BUILD_NAME        "@CMAKE_SYSTEM_NAME@-@CMAKE_SYSTEM_PROCESSOR@-AddressSanitizer")
set(CTEST_SITE              "@SITE@")

set(MODEL Experimental)
if(${CTEST_SCRIPT_ARG} MATCHES Nightly)
  set(MODEL Nightly)
elseif(${CTEST_SCRIPT_ARG} MATCHES Continuous)
  set(MODEL Continuous)
endif()

set(CTEST_MEMORYCHECK_TYPE "AddressSanitizer")
ctest_start(${MODEL})
ctest_configure(OPTIONS "-DPCAPPP_USE_SANITIZER=Address")
ctest_build()
ctest_memcheck(RETURN_VALUE test_result)

set(CDASH_TOKEN "$ENV{CTEST_CDASH_AUTH_TOKEN}")
if (NOT "${CDASH_TOKEN}" STREQUAL "")
	ctest_submit(HTTPHEADER "Authorization: Bearer ${CDASH_TOKEN}")
else()
  message("No CDASH Token found!!! Will not upload the result")
endif()

if (test_result)
  message(FATAL_ERROR "Test failed!")
endif ()
